#version 460 core
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_shader_image_load_formatted : enable

layout(binding = 0, set = 0) uniform accelerationStructureEXT TLAS; // Acceleration Structure
layout(binding = 1, set = 0) uniform image2D image;                 // Storage Image
layout(binding = 2, set = 0) uniform SceneUBO                       // Uniform Buffer for Scene data
{
	mat4 viewInverse;
	mat4 projInverse;
	vec3 camPosition; float pad0;
	vec3 lightPosition; float pad1;
    vec3 lightU; float pad2;
	vec3 lightV; float pad3;
} scene;

layout(location = 0) rayPayloadEXT vec3 hitValue;


void main()
{
    float tmin = 0.001;
    float tmax = 10000.0;

	vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);

	const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
	vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = scene.viewInverse * vec4(0, 0, 0, 1);
	vec4 target = scene.projInverse * vec4(d.x, d.y, 1, 1);
	vec4 direction = scene.viewInverse * vec4(normalize(target.xyz), 0);

	hitValue = vec3(1.0);
	traceRayEXT(
		TLAS,                  // Acceleration structure
		gl_RayFlagsOpaqueEXT,  // Ray flags
		0xff,                  // Cull mask
		0,                     // SBT record offset
		1,                     // SBT record stride
		0,                     // Miss shader index
		origin.xyz,            // Ray origin
		tmin,                  // Min ray distance
		direction.xyz,         // Ray direction
		tmax,                  // Max ray distance
		0                      // Ray payload location
	);

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 1.0));
}
