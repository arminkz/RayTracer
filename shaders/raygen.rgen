#version 460 core
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_shader_image_load_formatted : enable

layout(binding = 0, set = 0) uniform accelerationStructureEXT TLAS; // Acceleration Structure
layout(binding = 1, set = 0) uniform image2D image;                 // Storage Image
layout(binding = 2, set = 0) uniform CameraProperties               // Uniform Buffer for Camera
{
	mat4 viewInverse;
	mat4 projInverse;
} cam;

layout(location = 0) rayPayloadEXT vec3 hitValue;

// Number of samples per pixel (higher = better quality, slower) (Supersampling Anti-Aliasing) 
const int SAMPLES_PER_PIXEL = 1;

// Simple PCG random number generator
uint rngState;

uint pcg_hash(uint seed)
{
    uint state = seed * 747796405u + 2891336453u;
    uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
    return (word >> 22u) ^ word;
}

void initRNG(uvec2 pixelCoords, uint frameIndex)
{
    // Initialize RNG state based on pixel coordinates and frame index
    rngState = pcg_hash(pixelCoords.x + pcg_hash(pixelCoords.y + pcg_hash(frameIndex)));
}

float randomFloat()
{
    rngState = pcg_hash(rngState);
    return float(rngState) / float(0xffffffffu);
}

vec2 randomInUnitSquare()
{
    return vec2(randomFloat(), randomFloat());
}

void main()
{
    // Initialize random number generator (use 0 for frameIndex for now, can be passed via uniform for temporal AA)
    initRNG(gl_LaunchIDEXT.xy, 0);

    vec3 accumulatedColor = vec3(0.0);
    float tmin = 0.001;
    float tmax = 10000.0;

    // Shoot multiple rays per pixel with random jitter
    for (int i = 0; i < SAMPLES_PER_PIXEL; i++)
    {
        // Add random jitter within the pixel [-0.5, 0.5]
        //vec2 jitter = randomInUnitSquare() - 0.5;
        vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5); //+ jitter;

        const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
        vec2 d = inUV * 2.0 - 1.0;

        vec4 origin = cam.viewInverse * vec4(0, 0, 0, 1);
        vec4 target = cam.projInverse * vec4(d.x, d.y, 1, 1);
        vec4 direction = cam.viewInverse * vec4(normalize(target.xyz), 0);

        hitValue = vec3(1.0);
        traceRayEXT(TLAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);

        accumulatedColor += hitValue;
    }

    // Average the accumulated samples
    vec3 finalColor = accumulatedColor / float(SAMPLES_PER_PIXEL);

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(finalColor, 1.0));
}
